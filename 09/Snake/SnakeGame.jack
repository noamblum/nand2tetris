class SnakeGame {

    field Snake s;

    static int UP, DOWN, LEFT, RIGHT;

    constructor SnakeGame new(){
        let s = Snake.new(4,4,4);
        return this;
    }

    method void dispose() {
        do Memory.deAlloc(this);
        return;
   	}

    method void run() {

        var int direction;
        var char key;  // the key currently pressed by the user
        var boolean exit;
        let exit = false;

        do SnakeGame.printInstructions();
      
        while (~exit) {
            let key = Keyboard.keyPressed();
            let direction = -1;
            if (key = 81)  { let exit = true; }     // q key
            if (key = 131) { let direction = UP; }   // up arrow
            if (key = 133) { let direction = DOWN; }   // down arrow
            if (key = 130) { let direction = LEFT; }   // left arrow
            if (key = 132) { let direction = RIGHT; }   // right arrow
            if (~(direction = -1)) { do s.turn(direction); }
            do s.move();
            do Sys.wait(200);
        } // while
        return;
    }

    function void init () {
        let UP = 0;
        let DOWN = 1;
        let LEFT = 2;
        let RIGHT = 3;
        return;
    }

    function void printInstructions() {
        do Output.println();
        do Output.printString("Welcome to our");
        do Output.println();
        do Output.printString("  Snake game!");
        do Output.println();
        do Output.println();
        do Output.println();
        do Output.printString("  Control the");
        do Output.println();
        do Output.printString("snake with the");
        do Output.println();
        do Output.printString("  arrow keys");
        do Output.println();
        do Output.println();
        do Output.println();
        do Output.printString("Press q to exit");
        do Output.println();
        do Output.println();
        do Output.println();
        do Output.printString("  Good luck!");
        return;
    }
}
