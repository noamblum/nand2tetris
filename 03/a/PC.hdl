// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/03/a/PC.hdl

/**
 * A 16-bit counter with load and reset control bits.
 * if      (reset[t] == 1) out[t+1] = 0
 * else if (load[t] == 1)  out[t+1] = in[t]
 * else if (inc[t] == 1)   out[t+1] = out[t] + 1  (integer addition)
 * else                    out[t+1] = out[t]
 */

CHIP PC {
    IN in[16],load,inc,reset;
    OUT out[16];

    PARTS:
    Or(a=load, b=inc, out=loadOrinc);
    Or(a=loadOrinc, b=reset, out=anyBit);

    Inc16(in=prev, out=prevInc);
    Mux16(a=prev, b=prevInc, sel=inc, out=postInc);

    Mux16(a=postInc, b=in, sel=load, out=postLoad);

    Mux16(a=postLoad, b=false, sel=reset, out=finalIn);

    Register(in=finalIn, load=anyBit, out=prev, out=out);
}
